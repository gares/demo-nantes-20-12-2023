%%%%%%%%%%% syntax %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% terms
kind term type.

type global  string -> term.

type app term -> term -> term.
type lam (term -> term) -> term.
type let term -> ty -> (term -> term) -> term.
type eq  term -> term -> term.


% type expressions
kind tye type.

type (==>) tye -> tye -> tye.
type int   tye.
type bool  tye.
type list  tye -> tye.
type pair  tye -> tye -> tye.

% types
kind ty type.

type all    eq? -> (tye -> ty) -> ty.
type mono   tye -> ty.

kind eq? type.

type any eq?.
type eqt eq?.

%%%%%%%%%%%% typing %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

pred of i:term, o:ty.

of (global "1")      (mono int).
of (global "2")      (mono int).
of (global "3")      (mono int).
of (global "plus")   (mono (int ==> int ==> int)).

of (global "[]")    (all any x\ mono (list x)).
of (global "::")    (all any x\ mono (x ==> list x ==> list x)).
of (global "size")  (all any x\ mono (list x ==> int)).
of (global "undup") (all eqt x\ mono (list x ==> list x)).

of (global ",")     (all any x\ all any y\ mono (x ==> y ==> (pair x y))).

% accumulate hm.
% accumulate j.

%%%%%%%%%%%% type schema elimination %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
pred specialize i:ty, o:tye.

specialize (mono T) T.
specialize (all any F) T :- specialize (F Fresh_) T.
specialize (all eqt F) T :- specialize (F Fresh) T, eqbar Fresh.

pred eqbar i:tye.

eqbar bool.
eqbar int.
eqbar (list A) :- eqbar A.
eqbar (pair A B) :- eqbar A, eqbar B.

eqbar T :- var T, new_constraint (eqbar T) [T,_].

eqbar T :- eqtype-error T.

%%%%%%%%%%%%% type schema introduction %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% theta carries the list of type variables for which eqbar has to hold
pred theta i:list tye.

theta L :- new_constraint (theta L) [_].

% gammabar is not a real constraint, but rather a query to the meta level to
% compute a polymorphic type out of a monomorphic one and its context
pred gammabar i:ty, o:ty.

gammabar (mono T) TS :-
  new_constraint (gammabar (mono T) TS) [_].

% constraint store %
constraint of gammabar eqbar theta {

rule (theta L)                   % matched
     \  (G ?- gammabar T TS)     % matched and removed
     |  (generalize L G T POLYT) % guard + syntesis
    <=> (TS = POLYT).            % new goal

rule (eqbar V) \ (theta L) | (not(mem L V)) <=> (theta [V | L]).

}

pred generalize i:list tye, i:list prop, i:ty, o:ty.
generalize Theta Gamma (mono T) PolyT :-
  free-ty (mono T) [] VT,
  free-gamma Gamma [] VGamma,
  filter VT (x\ not(mem VGamma x)) ToQuantify,
  bind ToQuantify Theta T PolyT.

% computation of free (unification) variables %%%%%%%%%%%%%%%%%%%%%%%
pred free-ty i:ty, i:list tye, o:list tye.
free-ty (mono X) L L1 :- free X L L1.
free-ty (all _ F) L L1 :- pi x\ free-ty (F x) L L1.

pred free-gamma i:list prop, i:list tye, o:list tye.
free-gamma [] L L.
free-gamma [of _ T|X] L L2 :- free-ty T L L1, free-gamma X L1 L2.

pred free i:tye, i:list tye, o:list tye.
free int L L.
free bool L L.
free (list A) L L1 :- free A L L1.
free (pair A B) L L2 :- free A L L1, free B L1 L2.
free (A ==> B) L L2 :- free A L L1, free B L1 L2.
free (uvar _ _ as X) L L1 :- if (mem L X) (L1 = L) (L1 = [X|L]).

% quantification (binding) of a list of variables %%%%%%%%%%%%%%%%%%%%%%%
pred bind i:list tye, i:list tye, i:tye, o:ty.
bind [] _ T (mono T1) :- copy T T1.
bind [X|XS] Theta T (all E x\ T1 x) :-
  if (mem Theta X) (E = eqt) (E = any),
  pi c\ copy X c => bind XS Theta T (T1 c).

pred copy i:tye, o:tye.
copy int int.
copy bool bool.
copy (A ==> B) (A1 ==> B1) :- copy A A1, copy B B1.
copy (list A) (list A1) :- copy A A1.
copy (pair A B) (pair A1 B1) :- copy A A1, copy B B1.
copy (uvar U L) (uvar U L).

% boring stuff %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

pred filter i:list A, i:(A -> prop), o:list A.
filter [] _ [].
filter [X|XS] P [X|YS] :- P X, !, filter XS P YS.
filter [_|XS] P YS :- filter XS P YS.

pred mem! i:list A, o:A.
mem! [X|_] X :- !.
mem! [_|XS] X :- mem XS X.

% 'uvar X L' is the frozen 'X' and its arguments 'L'
pred mem i:list tye, i:tye.
mem L (uvar X _) :- mem! L (uvar X Fresh_).

pred assert i:term, i:tye, i:tye.
assert _ TY ETY :- TY = ETY, !.
assert T TY ETY :- print "KO: term (" {pp T} ") has type (" {ppt TY} ") but its context expects (" {ppt ETY} ")\n", halt.

pred eqtype-error i:tye.
eqtype-error T :- print "KO: type (" {ppt T} ") has no equality\n", halt.

pred new_constraint i:prop, i:A.
new_constraint P X :- declare_constraint P X.

pred no-parens.

pred fname i:string, o:string.
fname X F :-
  Rex is X ^ ".*",
  std.findall (std.mem {names} N, pp N S, rex.match Rex S) L,
  if (L = []) (F = X) (F is X ^ {std.any->string {std.length L}}).

pred pp i:term, o:string.
pp (lam F) S :- pi x\ fname "x" X, pp x X => S is "(fun " ^ {pp x} ^ " => " ^ {pp (F x)} ^ ")".
pp (let E uvar F) S :- pi x\ fname "f" X, pp x X => S is "let " ^ {pp x} ^ " = " ^ {pp E} ^ " in " ^ {pp (F x)}.
pp (let E Ty F) S :- pi x\ fname "f" X, pp x X => S is "let " ^ {pp x} ^ " : " ^ {ppty Ty} ^ " = " ^ {pp E} ^ " in " ^ {pp (F x)}.
pp (app (app (global ",") A) B) S :- S is "(" ^ {pp A} ^ ", " ^ {pp B} ^ ")".
pp (app (app (global "::") A) B) S :- S is "(" ^ {pp A} ^ " :: " ^ {pp B} ^ ")".
pp (app H A) S :- no-parens, S is {pp H} ^ " " ^ {pp A}.
pp (app (app _ _ as H) A) S :- S is "(" ^ {no-parens => pp H} ^ " " ^ {pp A} ^")".
pp (app H A) S :- S is "(" ^ {pp H} ^ " " ^ {pp A} ^ ")".
pp (global S) S.
pp (eq A B) S :- S is {pp A} ^ " = " ^ {pp B}.
pp (uvar X _) S :- std.any->string X S.
pp N S :- name N, std.any->string N S.


pred ppt i:tye, o:string.
ppt (A ==> B) S :- S is {ppt A} ^ " ==> " ^ {ppt B}.
ppt (pair A B) S :- S is {ppt A} ^ " * " ^ {ppt B}.
ppt (list A) S :- S is "list " ^ {ppt A}.
ppt int "int". 
ppt bool "bool". 
ppt (uvar X _) S :- std.any->string X S.

pred ppty i:ty, o:string.
ppty (mono T) S :- ppt T S.
ppty (all any F) S :- pi x\ fname "'a" A, ppt x A => ppty (F x) S.
ppty (all eqt F) S :- pi x\ fname "''a" A, ppt x A => ppty (F x) S.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% A bit too simple...
% of X (mono T) :- var X, new_constraint (of X (mono T)) [X].
% constraint of gammabar eqbar theta {
%   rule (of X T1) \ (of X T2) <=> (T1 = T2).
% }

pred err.
of X (mono ETY) :- not err, !, err => of X (mono TY), assert X TY ETY.
of X (mono _) :- print "KO: term (" {pp X} ") has no type\n", halt.

pred typecheck i:term.
typecheck T :-
  print "Checking:" {pp T},
  theta [],
  of T TY, !,
  print {pp T} " : " {ppty TY},
%  print, print_constraints,
  print.

pred tests i:list string.
tests [X] :- test X PL, typecheck PL.

main :- tests ["2"].

pred test i:string, o:term.
% 3 = 2
test "1" (eq (global "3") (global "2")).

% let id x = x in id []
test "2" (let (lam x\x) Ty_ id\
          app id (global "[]")).

% let id x = x in (id [], id 1)
test "3" (let (lam x\x) Ty_ id\
          app id (app (app (global ",")  (global "[]")) (global "1"))).

% let refl x = x = x in refl []
test "4" (let (lam x\ eq x x) Ty_ refl\
          app refl (global "[]")).

% let refl x = x = x in refl []
test "5" (let (lam x\ eq x x) Ty_ refl\
          app refl (global "1")).

test "6" (eq (app (app (global ",") (global "1")) (global "1"))
             (app (app (global ",") H)            H)).

test "-7" (eq (app (app (global ",") (global "1")) (global "[]"))
              (app (app (global ",") H)            H)).

% (fun x -> x) = (fun x -> x)
test "-1" (eq (lam x\x) (lam x\x)).

% let refl x = x = x in refl (fun x -> x)
test "-2" (let (lam x\ eq x x) Ty_ refl\
           app refl (lam x\x)).

% 1 = []
test "-3" (eq (global "1") (global "[]")).

% (1,1) = (fun x -> 2) 3
test "-4" (eq (app (app (global ",") (global "1")) (global "1"))
              (app (lam (_\global "2")) (global "3"))).

test "-5" (global "xxx").

% (fun x -> x x) (fun x -> x x)
test "-6" (app (lam x\app x x) (lam x\app x x)).
