%%%%%%%%%%% datatypes %%%%%%%%%%%%%%%

% terms
kind term type.

type literal int -> term.
type global  string -> term.

type app term -> term -> term.
type lam (term -> term) -> term.
type let term -> ty -> (term -> term) -> term.
type eq  term -> term -> term.


% type expressions
kind tye type.

type (==>) tye -> tye -> tye.
type int   tye.
type bool  tye.
type list  tye -> tye.
type pair  tye -> tye -> tye.

% types
kind eq? type.
type any eq?.
type eqt eq?.

kind ty type.

type all    eq? -> (tye -> ty) -> ty.
type mono   tye -> ty.
% List.rev: 'a list -> 'a list      == all any (a\ mono (list a ==> list a))
% List.mem: ''a -> ''a list -> bool == all eqt (a\ mono (a ==> list a ==> bool))
% Int.add : int -> int -> int       == mono (int ==> int ==> int)

%%%%%%%%%%%% w %%%%%%%%%%%%%%

% schemas
pred specialize i:ty, o:tye.

specialize (all any F) T :- specialize (F Fresh_) T.
specialize (all eqt F) T :- specialize (F Fresh) T, eqbar Fresh.
specialize (mono T) T.


%%%%%%%%%%%%% theta %%%%%%%%%%%%%%%%

pred theta i:list tye.

theta L :- declare_constraint (theta L) [_].

pred clear.
clear :- declare_constraint clear [_].

constraint w gammabar eqbar theta clear {

rule (eqbar V) \ (theta L) | (not(mem-theta L V)) <=> (theta (V :: L)).
rule clear \ (theta _).
rule clear \ (eqbar _).
rule \ clear.

% 'uvar X L' is the frozen 'X' and its arguments 'L'
pred mem-theta i:list tye, i:tye.
mem-theta [uvar X _| _] (uvar X _).
mem-theta [uvar _ _| XS] Y :- mem-theta XS Y.

}

%%%%%%%%%%%%% eqbar %%%%%%%%%%%%%%%%

pred eqbar i:tye.

eqbar bool.
eqbar int.
eqbar (list A) :- eqbar A.
eqbar (pair A B) :- eqbar A, eqbar B.

eqbar T :- var T, declare_constraint (eqbar T) [T,_].

eqbar T :- eqtype-error T.

%%%%%%%%%%%% gammabar %%%%%%%%%%%%%

pred gammabar i:ty, o:ty.

gammabar (mono T) TS :-
  declare_constraint (gammabar (mono T) TS) [_].

constraint w gammabar eqbar theta clear {

rule (theta L)                   % matched
     \  (G ?- gammabar T TS)     % matched and removed
     |  (generalize L G T POLYT) % guard + syntesis
    <=> (TS = POLYT).            % new goal

pred generalize i:list tye, i:list prop, i:ty, o:ty.
generalize Theta Gamma (mono T) PolyT :-
  free-ty (mono T) [] VT,
  free-gamma Gamma [] VGamma,
  filter VT (x\ not(mem VGamma x)) ToQuantify,
  bind ToQuantify Theta T PolyT.

% computation of free (unification) variables
pred free-ty i:ty, i:list tye, o:list tye.
free-ty (mono X) L L1 :- free X L L1.
free-ty (all _ F) L L1 :- pi x\ free-ty (F x) L L1.

pred free-gamma i:list prop, i:list tye, o:list tye.
free-gamma [] L L.
free-gamma [w _ T|X] L L2 :- free-ty T L L1, free-gamma X L1 L2.

pred free i:tye, i:list tye, o:list tye.
free (list A) L L1 :- free A L L1.
free (pair A B) L L2 :- free A L L1, free B L1 L2.
free (A ==> B) L L2 :- free A L L1, free B L1 L2.
free (uvar X _) L L1 :- if (mem L X) (L1 = L) (L1 = [X|L]).
free _ L L.

% quantification (binding) of a list of variables
pred bind i:list tye, i:list tye, i:tye, o:ty.
bind [] _ T (mono T1) :- copy T T1.
bind [X|XS] Theta T (all E x\ T1 x) :-
  if (mem-theta Theta (uvar X _)) (E = eqt) (E = any),
  pi c\ copy (uvar X _) c => bind XS Theta T (T1 c).

pred copy i:tye, o:tye.
copy int int.
copy bool bool.
copy (A ==> B) (A1 ==> B1) :- copy A A1, copy B B1.
copy (list A) (list A1) :- copy A A1.
copy (pair A B) (pair A1 B1) :- copy A A1, copy B B1.
copy (uvar U L) (uvar U L).

}

% {{{ stdlib: mem, filter, if...

pred filter i:list A, i:(A -> prop), o:list A.
filter [] _ [].
filter [X|XS] P [X|YS] :- P X, !, filter XS P YS.
filter [_|XS] P YS :- filter XS P YS.

pred mem i:list A, i:A.
mem [X|_] X :- !.
mem [_|XS] X :- mem XS X.

pred assert i:term, i:tye, i:tye.
assert _ TY ETY :- TY = ETY, !.
assert T TY ETY :- print "KO: term (" T ") has type (" TY ") but its context expects (" ETY ")\n", halt.

pred eqtype-error i:tye.
eqtype-error T :- print "KO: type (" T ") has no equality\n", halt.

% }}}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% constants

w (literal _)      (mono int).
w (global "plus")  (mono (int ==> int ==> int)).

w (global "[]")    (all any x\ mono (list x)).
w (global "::")    (all any x\ mono (x ==> list x ==> list x)).
w (global "size")  (all any x\ mono (list x ==> int)).
w (global "undup") (all eqt x\ mono (list x ==> list x)).

w (global ",")     (all any x\ all any y\ mono (x ==> y ==> (pair x y))).

pred err.
w X (mono ETY) :- not err, !, err => w X (mono TY), assert X TY ETY.
w X (mono _) :- print "KO: term (" X ") has no type\n", halt.

pred typecheck i:term.
typecheck T :-
  print "Checking:" T,
  theta [],
  w T TY, !,
  clear,
  print T " : " TY,
  print.

pred tests i:list string.
tests L :- std.map L test PL, std.forall PL typecheck.

pred test i:string, o:term.
% 3 = 2
test "1" (eq (literal 3) (literal 2)).

% let id x = x in id []
test "2" (let (lam x\x) Ty_ id\
          app id (global "[]")).

% let id x = x in (id [], id 1)
test "3" (let (lam x\x) Ty_ id\
          app id (app (app (global ",")  (global "[]")) (literal 1))).

% let refl x = x = x in refl []
test "4" (let (lam x\ eq x x) Ty_ refl\
          app refl (global "[]")).

% let refl x = x = x in refl []
test "5" (let (lam x\ eq x x) Ty_ refl\
          app refl (literal 1)).


% (fun x -> x) = (fun x -> x)
test "-1" (eq (lam x\x) (lam x\x)).

% let refl x = x = x in refl (fun x -> x)
test "-2" (let (lam x\ eq x x) Ty_ refl\
           app refl (lam x\x)).

test "-3" (eq (literal 1) (global "[]")).

test "-4" (eq (app (app (global ",") (literal 1)) (literal 1))
              (app (lam (_\literal 2)) (literal 3))).

test "-5" (global "xxx").

test "-6" (app (lam x\app x x) (lam x\app x x)).


  % let card x = size (undup x) in card []
  % let f y = let g x = (x,y) in g y in f 1
  % size 1
  % [1] = (1,1)